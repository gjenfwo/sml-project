# This file is part of the Soletta Project
#
# Copyright (C) 2015 Intel Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

INPORT=start_conv.IN:START
INPORT=stop_conv.IN:STOP
INPORT=end_conv.IN:RESET
OUTPORT=concat.OUT:OUT

false(constant/boolean:value=false)
ten(constant/int:value=10)

false OUT -> ENABLED timer(timer:interval=100)
timer OUT -> INC sec(int/accumulator:setup_value=min:0|max:59|step:1)
start_conv(converter/empty-to-boolean:output_value=true) OUT -> ENABLED timer
end_conv(converter/empty-to-boolean:output_value=false) OUT -> ENABLED timer
stop_conv(converter/empty-to-boolean:output_value=false) OUT -> ENABLED timer

end_conv OUT -> PULSE_IF_FALSE reset_pulse(converter/boolean-to-empty)
reset_pulse OUT -> RESET sec
reset_pulse OUT -> RESET min

sec OVERFLOW -> INC min(int/accumulator:setup_value=min:0|step:1)


ten OUT -> IN[0] sec_cmp(int/greater)
sec OUT -> IN[1] sec_cmp OUT -> IN custom_str(converter/boolean-to-string:false_value=":",true_value=":0")
custom_str OUT -> IN[0] sec_concat(string/concatenate)
sec OUT -> IN sec_str(converter/int-to-string) OUT -> IN[1] sec_concat

min OUT -> IN min_str(converter/int-to-string) OUT -> IN[0] concat(string/concatenate)
sec_concat OUT -> IN[1] concat
